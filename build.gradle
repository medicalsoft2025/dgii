plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0-SNAPSHOT'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.medical'
version = '0.0.1-SNAPSHOT'
description = 'integration to onepay'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    // Configuración específica para las herramientas de JAXB
    jaxb
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // Módulo de Jackson para entender anotaciones JAXB
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations'


    // Apache HttpClient (versiones compatibles)
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpmime:4.5.14'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'

    // XML Parser Oracle (OBLIGATORIO para DGII)
    implementation 'com.oracle.database.xml:xmlparserv2:21.8.0.0'

    // Dependencias de JAXB (necesarias para Java 9+)
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.0'

    // Dependencias para la herramienta de generación de código JAXB
    jaxb 'com.sun.xml.bind:jaxb-xjc:4.0.0'
    jaxb 'com.sun.xml.bind:jaxb-impl:4.0.0'


    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Testcontainers
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    testImplementation 'org.testcontainers:rabbitmq:1.19.8'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

// Tarea personalizada para generar clases desde XSD con JAXB
task genJaxb {
    ext.sourcesDir = file("${buildDir}/generated-sources/jaxb")
    ext.xsdDir31 = file("src/main/resources/xsd/ecf31")
    ext.xsdDir32 = file("src/main/resources/xsd/ecf32")
    ext.xsdDir33 = file("src/main/resources/xsd/ecf33")
    ext.xsdDir34 = file("src/main/resources/xsd/ecf34")

    outputs.dir sourcesDir

    doFirst {
        // Forzar la limpieza del directorio de salida antes de generar
        delete(sourcesDir)
        mkdir(sourcesDir)
    }

    doLast {
        project.ant {
            taskdef name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.jaxb.asPath
            
            echo(message: "Generando clases JAXB para ECF v31...")
            xjc(destdir: sourcesDir, schema: "${xsdDir31}/e-CF 31 v.1.0.xsd") {
                arg(value: "-p")
                arg(value: "com.medical.onepay.core.dgii.ecf.v31")
            }

            echo(message: "Generando clases JAXB para ECF v32...")
            xjc(destdir: sourcesDir, schema: "${xsdDir32}/e-CF 32 v.1.0.xsd") {
                arg(value: "-p")
                arg(value: "com.medical.onepay.core.dgii.ecf.v32")
            }

            echo(message: "Generando clases JAXB para ECF v33...")
            xjc(destdir: sourcesDir, schema: "${xsdDir33}/e-CF 33 v.1.0.xsd") {
                arg(value: "-p")
                arg(value: "com.medical.onepay.core.dgii.ecf.v33")
            }

            echo(message: "Generando clases JAXB para ECF v34...")
            xjc(destdir: sourcesDir, schema: "${xsdDir34}/e-CF 34 v.1.0.xsd") {
                arg(value: "-p")
                arg(value: "com.medical.onepay.core.dgii.ecf.v34")
            }
        }
    }
}

// Asegurar que genJaxb se ejecute antes de compilar Java
compileJava.dependsOn genJaxb

// Añadir las clases generadas al conjunto de fuentes de Java
sourceSets.main.java.srcDir file("${buildDir}/generated-sources/jaxb")

tasks.named('test') {
    useJUnitPlatform()
}
